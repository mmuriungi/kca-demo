page 52087 "Timetable Header"
{
    ApplicationArea = All;
    Caption = 'Timetable Header';
    PageType = Card;
    SourceTable = "Timetable Header";
    PromotedActionCategories = 'New,Process,Reports,Constraints';

    layout
    {
        area(Content)
        {
            group(General)
            {
                Caption = 'General';

                field("Document No."; Rec."Document No.")
                {
                    ToolTip = 'Specifies the value of the Document No. field.', Comment = '%';
                    Importance = Promoted;
                }
                field("Academic Year"; Rec."Academic Year")
                {
                    ToolTip = 'Specifies the value of the Academic Year field.', Comment = '%';
                }
                field(Semester; Rec.Semester)
                {
                    ToolTip = 'Specifies the value of the Semester field.', Comment = '%';
                }
                field("Generated By"; Rec."Generated By")
                {
                    ToolTip = 'Specifies the value of the Generated By field.', Comment = '%';
                }
                field("Exam Type"; Rec."Exam Type")
                {
                    ToolTip = 'Specifies the value of the Exam Type field.', Comment = '%';
                }
                field("Timetable Status"; Rec."Timetable Status")
                {
                    ToolTip = 'Specifies the value of the Timetable Status field.', Comment = '%';
                }
                field("Linked Timetable No."; Rec."Linked Timetable No.")
                {
                    ToolTip = 'Specifies the value of the Linked Timetable No. field.', Comment = '%';
                }
                field("Change Type"; Rec."Change Type")
                {
                    ToolTip = 'Specifies the type of change made to this timetable.', Comment = '%';
                    Visible = Rec."Linked Timetable No." <> '';
                }
                field("Change Reason"; Rec."Change Reason")
                {
                    ToolTip = 'Specifies the reason for changes made to this timetable.', Comment = '%';
                    Visible = Rec."Linked Timetable No." <> '';
                    MultiLine = true;
                }
                field("Has Changes"; Rec."Has Changes")
                {
                    ToolTip = 'Indicates if this timetable has tracked changes.', Comment = '%';
                    Visible = Rec."Linked Timetable No." <> '';
                    Editable = false;
                    Style = Attention;
                    StyleExpr = Rec."Has Changes";
                }
            }
            group(ExamGroup)
            {
                Caption = 'Exam Controls';
                Visible = Rec."Type" = Rec."Type"::Exam;
                field("Start Date"; Rec."Start Date")
                {
                    ToolTip = 'Specifies the value of the Start Date field.', Comment = '%';
                }
                field("End Date"; Rec."End Date")
                {
                    ToolTip = 'Specifies the value of the End Date field.', Comment = '%';
                }
                field("Group Description"; Rec."Group Description")
                {
                    ToolTip = 'Specifies the value of the Group Description field.', Comment = '%';
                }
                field("Year Filter"; Rec."Year Filter")
                {
                    ToolTip = 'Specifies the value of the Year Filter field.', Comment = '%';
                }
                field("School Filter"; Rec."School Filter")
                {
                    ToolTip = 'Specifies the value of the School Filter field.', Comment = '%';
                    trigger OnDrillDown()
                    var
                        sFilters: Text;
                        DimValList: Page "Dimension Value List";
                        DimValRec: Record "Dimension Value";
                    begin
                        DimValRec.Reset();
                        DimValRec.SetRange("Global Dimension No.", 3);
                        if DimValRec.FindSet() then begin
                            DimValList.LookupMode(true);
                            DimValList.SetTableView(DimValRec);
                            if DimValList.RunModal = ACTION::LookupOK then begin
                                sFilters := DimValList.GetSelectionFilter;
                            end;
                        end;

                        DimValRec.Reset();
                        DimValRec.SetRange("Global Dimension No.", 3);
                        DimValRec.SetFilter(Code, sFilters);
                        if DimValRec.FindSet() then begin
                            repeat
                                Rec."School Filter" += DimValRec.Code + ',';
                            until DimValRec.Next() = 0;
                        end;
                    end;
                }
                field("Programme Filter"; Rec."Exam Programme Filter")
                {
                    ToolTip = 'Specifies the value of the Programme Filter field.', Comment = '%';
                    trigger OnDrillDown()
                    var
                        sFilters: Text;
                        DimValList: Page "ACA-Programmes List";
                        ProgrammeRec: Record "ACA-Programme";
                    begin
                        ProgrammeRec.Reset();
                        if ProgrammeRec.FindSet() then begin
                            DimValList.LookupMode(true);
                            DimValList.SetTableView(ProgrammeRec);
                            if DimValList.RunModal = ACTION::LookupOK then begin
                                sFilters := DimValList.GetSelectionFilter;
                            end;
                        end;
                        ProgrammeRec.Reset();
                        ProgrammeRec.SetFilter(Code, sFilters);
                        if ProgrammeRec.FindSet() then begin
                            repeat
                                Rec."Exam Programme Filter" += ProgrammeRec.Code + ',';
                            until ProgrammeRec.Next() = 0;
                        end;
                    end;
                }
                field("Exclude Years"; Rec."Exclude Years")
                {
                    ToolTip = 'Specifies the value of the Exclude Years field.', Comment = '%';
                }
                field("Exclude Schools"; Rec."Exclude Schools")
                {
                    ToolTip = 'Specifies the value of the Exclude Schools field.', Comment = '%';
                }
                field("Exclude Programmes"; Rec."Exclude Programmes")
                {
                    ToolTip = 'Specifies the value of the Exclude Programmes field.', Comment = '%';
                }
            }
            part(timetableEntry; "Timetable Entry")
            {
                Visible = Rec."Type" <> Rec."Type"::Exam;
                ApplicationArea = All;
                Caption = 'Timetable Entry';
                SubPageLink = Semester = field(Semester), "Document No." = field("Document No.");
            }
            part(examTimetableEntry; "Exam Timetable Entry")
            {
                Visible = Rec."Type" = Rec."Type"::Exam;
                SubPageLink = Semester = field(Semester), "Exam Type" = field("Exam Type"), "Document No." = field("Document No.");
                ApplicationArea = All;
                Caption = 'Exam Timetable Entry';
            }
        }
    }
    actions
    {
        area(Navigation)
        {
            action("Lecturer Timetable Constraints")
            {
                ApplicationArea = All;
                Caption = 'Lecturer Timetable Constraints';
                RunObject = Page "Lecturer Timetable Constraints";
                RunPageLink = Semester = field(Semester);
                Visible = rec.Type = rec.Type::Class;
                Image = ConditionalBreakpoint;
                Promoted = true;
                PromotedCategory = Category4;
            }
            action("Online Preferences")
            {
                ApplicationArea = All;
                Caption = 'Online Preferences';
                RunObject = Page "Online Preferences";
                Visible = rec.Type = rec.Type::Class;
                Image = ConditionalBreakpoint;
                Promoted = true;
                PromotedCategory = Category4;
            }
        }
        area(Processing)
        {
            action(GenerateTimetable)
            {
                ApplicationArea = All;
                Caption = 'Generate Timetable';
                Promoted = true;
                PromotedCategory = Process;
                Visible = rec.Type = rec.Type::Class;
                PromotedIsBig = true;
                Image = GetLines;
                trigger OnAction()
                var
                    TtCu: codeunit "Timetable Management";
                begin
                    TtCu.GenerateTimetable(Rec);
                end;
            }
            action(GenerateExamTimetableByGroup)
            {
                ApplicationArea = All;
                Caption = 'Generate Exam Timetable By Group';
                Promoted = true;
                PromotedCategory = Process;
                Visible = (rec.Type = rec.Type::Exam) and (rec."Exam Type" = rec."Exam Type"::Regular);
                PromotedIsBig = true;
                Image = GetLines;
                trigger OnAction()
                var
                    TtCu: codeunit "Timetable Management";
                begin
                    TtCu.GenerateExamTimetableByGroup(
                        Rec,
                        Rec.Semester,
                        Rec."Start Date",
                        Rec."End Date",
                        Rec."Group Description",
                        Rec."Year Filter",
                        Rec."School Filter",
                        Rec."Exam Programme Filter",
                        Rec."Exclude Years",
                        Rec."Exclude Schools",
                        Rec."Exclude Programmes");
                end;
            }
            action(GenerateExamTimetable)
            {
                ApplicationArea = All;
                Caption = 'Generate Exam Timetable';
                Promoted = true;
                PromotedCategory = Process;
                Visible = (rec.Type = rec.Type::Exam) and (rec."Exam Type" = rec."Exam Type"::Regular);
                PromotedIsBig = true;
                Image = GetLines;
                trigger OnAction()
                var
                    TtCu: codeunit "Timetable Management";
                begin
                    TtCu.GenerateExamTimetable(Rec.Semester);
                end;
            }
            action(GenerateSupplementaryExamTimetable)
            {
                ApplicationArea = All;
                Caption = 'Generate Supplementary Exam Timetable';
                Promoted = true;
                PromotedCategory = Process;
                Visible = (rec.Type = rec.Type::Exam) and (rec."Exam Type" = rec."Exam Type"::Supplementary);
                PromotedIsBig = true;
                Image = GetLines;
                trigger OnAction()
                var
                    TtCu: codeunit "Timetable Management";
                begin
                    TtCu.GenerateSuppTimetable(Rec.Semester);
                end;
            }
            action(GenerateExamTimeSlots)
            {
                ApplicationArea = All;
                Caption = 'Generate Exam Time Slots';
                Promoted = true;
                PromotedCategory = Process;
                PromotedIsBig = true;
                Visible = (rec.Type = rec.Type::Exam) and (rec."Exam Type" = rec."Exam Type"::Regular);
                Image = GetLines;
                trigger OnAction()
                var
                    TtCu: codeunit "Timetable Management";
                begin
                    TtCu.GenerateExamTimeSlots(Rec);
                end;
            }
            action(GenerateSupplementaryExamTimeSlots)
            {
                ApplicationArea = All;
                Caption = 'Generate Supplementary Exam Time Slots';
                Promoted = true;
                PromotedCategory = Process;
                PromotedIsBig = true;
                Visible = (rec.Type = rec.Type::Exam) and (rec."Exam Type" = rec."Exam Type"::Supplementary);
                Image = GetLines;
                trigger OnAction()
                var
                    TtCu: codeunit "Timetable Management";
                begin
                    TtCu.GenerateSuppExamTimeSlots(Rec.Semester);
                end;
            }
            action("Timetable Report")
            {
                ApplicationArea = All;
                Caption = 'Timetable Report';
                Promoted = true;
                PromotedCategory = Process;
                PromotedIsBig = true;
                Image = GetLines;
                Visible = rec.Type = rec.Type::Class;
                trigger OnAction()
                var
                    THeader: Record "Timetable Header";
                    Tentry: Record "Timetable Entry";
                begin
                    // THeader.Reset();
                    // THeader.SetRange(Semester, Rec.Semester);
                    // if THeader.FindFirst() then
                    //Report.Run(Report::"Class Timetable Report", TRUE, FALSE, THeader);
                    Tentry.Reset();
                    Tentry.SetRange(Semester, Rec.Semester);
                    Tentry.SetRange("Document No.", Rec."Document No.");
                    if Tentry.FindFirst() then
                        Report.Run(Report::"Class Timetable New", TRUE, FALSE, Tentry);
                end;
            }

            action("New Exam Timetable Report")
            {
                ApplicationArea = All;
                Caption = 'New Exam Timetable Report';
                Promoted = true;
                PromotedCategory = Process;
                PromotedIsBig = true;
                Image = GetLines;
                Visible = rec.Type = rec.Type::Exam;
                trigger OnAction()
                var
                    TTEntry: Record "Exam Timetable Entry";
                begin
                    TTEntry.Reset();
                    TTEntry.SetRange(Semester, Rec.Semester);
                    TTEntry.SetRange("Exam Type", Rec."Exam Type");
                    if TTEntry.FindFirst() then
                        Report.Run(Report::"Exam Timetable Report", TRUE, FALSE, TTEntry);
                end;
            }
            action("Examinations Timetable")
            {
                ApplicationArea = All;
                Caption = 'Examinations Timetable';
                Promoted = true;
                PromotedCategory = Process;
                PromotedIsBig = true;
                Image = Report;
                Visible = rec.Type = rec.Type::Exam;
                trigger OnAction()
                var
                    TTEntry: Record "Exam Timetable Entry";
                begin
                    TTEntry.Reset();
                    TTEntry.SetRange(Semester, Rec.Semester);
                    TTEntry.SetRange("Exam Type", Rec."Exam Type");
                    if Rec."Document No." <> '' then
                        TTEntry.SetRange("Document No.", Rec."Document No.");
                    if TTEntry.FindFirst() then
                        Report.Run(Report::"Examinations Timetable", TRUE, FALSE, TTEntry);
                end;
            }
            action("Exam Timetable Report")
            {
                ApplicationArea = All;
                Caption = 'Exam Timetable Report';
                Promoted = true;
                PromotedCategory = Process;
                PromotedIsBig = true;
                Image = GetLines;
                Visible = rec.Type = rec.Type::Exam;
                trigger OnAction()
                var
                    THeader: Record "Timetable Header";
                begin
                    THeader.Reset();
                    THeader.SetRange(Semester, Rec.Semester);
                    if THeader.FindFirst() then
                        Report.Run(Report::"Exam Timetable", TRUE, FALSE, THeader);
                end;
            }
            action("Copy from Linked Timetable")
            {
                ApplicationArea = All;
                Caption = 'Copy from Linked Timetable';
                Promoted = true;
                PromotedCategory = Process;
                PromotedIsBig = true;
                Image = Copy;
                Visible = (Rec."Linked Timetable No." <> '') and (Rec.Type = Rec.Type::Class);
                trigger OnAction()
                var
                    SourceHeader: Record "Timetable Header";
                    TimetableChangeMgt: Codeunit "Timetable Change Management";
                begin
                    if Rec."Linked Timetable No." = '' then
                        Error('No linked timetable specified.');

                    SourceHeader.Reset();
                    SourceHeader.SetRange(Semester, Rec.Semester);
                    SourceHeader.SetRange("Document No.", Rec."Linked Timetable No.");
                    if not SourceHeader.FindFirst() then
                        Error('Source timetable %1 not found.', Rec."Linked Timetable No.");

                    if Confirm('This will copy all entries from timetable %1. Continue?', false, Rec."Linked Timetable No.") then begin
                        TimetableChangeMgt.CopyTimetableWithModifications(SourceHeader, Rec);
                        CurrPage.Update(false);
                    end;
                end;
            }
            action("View Change Log")
            {
                ApplicationArea = All;
                Caption = 'View Change Log';
                Promoted = true;
                PromotedCategory = Category5;
                PromotedIsBig = true;
                Image = ViewDetails;
                Visible = Rec."Has Changes";
                RunObject = Page "Timetable Change Log";
                RunPageLink = "Timetable Document No." = field("Document No.");
                // trigger OnAction()
                // var
                //     ChangeLog: Record "Timetable Change Log";
                // begin
                //     ChangeLog.SetRange("Timetable Document No.", Rec."Document No.");
                //     Page.RunModal(52133, ChangeLog);
                // end;
            }
            action("Mark Lecturer Unavailable")
            {
                ApplicationArea = All;
                Caption = 'Mark Lecturer Unavailable';
                Promoted = true;
                PromotedCategory = Category5;
                Image = Absence;
                Visible = (Rec."Linked Timetable No." <> '') and (Rec.Type = Rec.Type::Class);
                trigger OnAction()
                var
                    LecturerCode: Code[20];
                    FromDate: Date;
                    ToDate: Date;
                    Reason: Text[250];
                begin
                    if Rec."Linked Timetable No." = '' then
                        Error('This action is only available for modified timetables.');

                    // Get lecturer unavailability details from user
                    // TODO: Create lecturer unavailability dialog page
                    Message('Lecturer unavailability functionality will be implemented. This will allow marking lecturers as unavailable for specific dates and automatically suggest alternatives.');
                end;
            }
            action("Define Change Rules")
            {
                ApplicationArea = All;
                Caption = 'Define Change Rules';
                Promoted = true;
                PromotedCategory = Category5;
                PromotedIsBig = true;
                Image = Setup;
                Visible = (Rec."Linked Timetable No." <> '') and (Rec.Type = Rec.Type::Class);
                RunObject = Page "Timetable Change Rules";
                RunPageLink = "Timetable Document No." = field("Document No.");
            }
            action("Apply Automated Changes")
            {
                ApplicationArea = All;
                Caption = 'Apply Automated Changes';
                Promoted = true;
                PromotedCategory = Process;
                PromotedIsBig = true;
                Image = ChangeLog;
                Visible = (Rec."Linked Timetable No." <> '') and (Rec.Type = Rec.Type::Class);
                trigger OnAction()
                var
                    ChangeMgt: Codeunit "Timetable Change Management";
                    ChangeRule: Record "Timetable Change Rule";
                begin
                    ChangeRule.SetRange("Timetable Document No.", Rec."Document No.");
                    ChangeRule.SetRange(Active, true);
                    ChangeRule.SetRange(Applied, false);

                    if ChangeRule.IsEmpty() then
                        Error('No active change rules defined. Please define rules first.');

                    if Confirm('Do you want to preview the changes before applying them?', true) then begin
                        ChangeMgt.ProcessChangeRules(Rec."Document No.", true);
                        if Confirm('Do you want to apply these changes now?', true) then
                            ChangeMgt.ProcessChangeRules(Rec."Document No.", false);
                    end else
                        ChangeMgt.ProcessChangeRules(Rec."Document No.", false);

                    CurrPage.Update(false);
                end;
            }
        }
    }
}
